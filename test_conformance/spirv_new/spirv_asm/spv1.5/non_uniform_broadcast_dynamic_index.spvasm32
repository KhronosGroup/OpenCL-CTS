; SPIR-V
; Version: 1.5
; Reference:
;   kernel void non_uniform_broadcast_dynamic_index_test(global uint* dst_base) {
;       uint id = get_global_id(0);
;       uint index = get_group_id(0);
;       uint value = sub_group_non_uniform_broadcast(id, index);
;       dst_base[id] = value;
;   }
               OpCapability Addresses
               OpCapability Kernel
               OpCapability GroupNonUniformBallot
               OpMemoryModel Physical32 OpenCL
               OpEntryPoint Kernel %kernel "non_uniform_broadcast_dynamic_index_test" %pglobalid %pgroupid
               OpDecorate %pglobalid BuiltIn GlobalInvocationId
               OpDecorate %pgroupid BuiltIn WorkgroupId
       %uint = OpTypeInt 32 0
   %sg_scope = OpConstant %uint 3
      %uint3 = OpTypeVector %uint 3
       %void = OpTypeVoid
%iptr_uint3 = OpTypePointer Input %uint3
  %gptr_uint = OpTypePointer CrossWorkgroup %uint
 %kernel_sig = OpTypeFunction %void %gptr_uint
  %pglobalid = OpVariable %iptr_uint3 Input
   %pgroupid = OpVariable %iptr_uint3 Input
     %kernel = OpFunction %void None %kernel_sig
   %dst_base = OpFunctionParameter %gptr_uint
      %entry = OpLabel
   %globalid = OpLoad %uint3 %pglobalid Aligned 32
         %id = OpCompositeExtract %uint %globalid 0
    %groupid = OpLoad %uint3 %pgroupid Aligned 32
      %index = OpCompositeExtract %uint %groupid 0
      %value = OpGroupNonUniformBroadcast %uint %sg_scope %id %index
        %dst = OpInBoundsPtrAccessChain %gptr_uint %dst_base %id
               OpStore %dst %value
               OpReturn
               OpFunctionEnd
